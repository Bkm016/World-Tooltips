buildscript {
	repositories {
		jcenter()
		maven { url = "http://files.minecraftforge.net/maven" }
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
	}
}

plugins {
	id "se.bjurr.gitchangelog.git-changelog-gradle-plugin" version "1.32"
}

apply plugin: 'net.minecraftforge.gradle.forge'

archivesBaseName = "World-Tooltips"

group = "ninja.genuine.world-tooltips"
ext.revision = 'git rev-list --count HEAD'.execute().text.trim()
version = "1.11-1.2.3-${revision}"

sourceCompatibility = targetCompatibility = "1.8"

minecraft {
	version = "1.11-13.19.1.2189"
	runDir = "run"
	mappings = "snapshot_20161217"
}

dependencies {
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

allprojects {
	tasks.withType(Javadoc) {
		options.addStringOption('Xdoclint:none', '-quiet')
	}
}

task makeChangelog(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
	filePath = "changelog.html"
	untaggedName = "Current release ${project.version}"
	fromCommit = "a06ed210a99b62ad632379308c691ef7978a496a"
	toRef =  "HEAD"
	templateContent = """
{{#tags}}
  <h3>{{name}}</h3>
  <ul>
    {{#commits}}
    <li>{{{message}}}</li>
    {{/commits}}
  </ul>
{{/tags}}
"""
}

task deobfJar(type: Jar, dependsOn: 'classes') {
	from sourceSets.main.output
	classifier = 'dev'
}

task javadocJar(type: Jar, dependsOn: 'javadoc') {
	from javadoc.destinationDir
	classifier = 'javadoc'
}

tasks.build.dependsOn makeChangelog, deobfJar, sourceJar
